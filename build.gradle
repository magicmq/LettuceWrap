plugins {
    id 'java'
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.0.0'
}

group = 'com.github.magicmq'
version = '1.5-SNAPSHOT'

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        shadow(MavenPublication) {
            publication -> project.shadow.component(publication)
        }
    }
    repositories {
        maven {
            def releasesRepo = 'https://magicmq.dev/nexus/repository/maven-releases/'
            def snapshotsRepo = 'https://magicmq.dev/nexus/repository/maven-snapshots/'
            url = version.endsWith('SNAPSHOT') ? snapshotsRepo : releasesRepo
            credentials {
                username = "${repo_username}"
                password = "${repo_password}"
            }
        }
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://papermc.io/repo/repository/maven-public/'
    }
}

dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.12'
    compileOnly 'com.destroystokyo.paper:paper-api:1.12.2-R0.1-SNAPSHOT'
    compile 'io.lettuce:lettuce-core:5.2.2.RELEASE'
}

import org.apache.tools.ant.filters.ReplaceTokens;

processResources {
    from (sourceSets.main.resources) {
        include 'plugin.yml'
        filter(ReplaceTokens, tokens: ['VERSION' : project.version])
    }
}

shadowJar {
    baseName = 'LettuceWrap'
    version = project.version
    classifier = null
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}


